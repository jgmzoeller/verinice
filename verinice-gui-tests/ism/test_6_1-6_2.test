--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _fKpG0MvFEei8QokgLeP3Yw,_HQDeEI9NEeic2YXgdvyDUQ,_tlZuIIx1Eei2UKUDmonytQ,_lGnj8Ix1Eei2UKUDmonytQ,_mCQwkI9ZEeic2YXgdvyDUQ
Element-Name: test_6_1-6_2
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _xUiAIMeuEeipUK6ek91lPA
Runtime-Version: 2.3.0.201806262310
Save-Time: 11/19/18 1:21 PM
Tags: ISM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "edit-empty-row" [val offset] [val selection1] [val selection2] [val selection3] {
	with [get-editor "Neue Abfrage [UNGÜLTIG]"] {
		get-combo -after [get-label "Verknüpfungen"] -index $offset | select $selection1
		get-combo -after [get-label "Verknüpfungen"] -index [plus $offset 1] | select $selection2
		get-combo -after [get-label "Verknüpfungen"] -index [plus $offset 3] | select $selection3
	}
}
proc "copy-edit-last" [val offset] [val selection] {
	with [get-editor "Neue Abfrage"] {
		get-button "Letzte Spalte kopieren" | click
		get-combo -after [get-label "Verknüpfungen"] -index $offset | select $selection
		get-button "Leere Spalte hinzufügen" | click
	}
}
proc "try-dont-switch" {
	try -command {
		get-window "Wechseln zur zugeordneten Perspektive" | get-button Nein | click
	} -catch {
	}
}
proc "does-exist-in-other-perspecive" [val viewPathName] [val view] [val path] [val caption] {
	get-menu -path [concat "Ansicht/Zeige View.../" $viewPathName] | click
	try-dont-switch
	get-view $view | get-tree | get-item -path $path | get-property caption | equals $caption | verify-true
	get-view $view | close
}
proc "wait-for-imports-or-fail" {
	try -times 40 -delay 10000 -command {
		get-view ISM | get-tree | get-item -path "importierte Objekte/Beispiel: NoKyTo GmbH 2018 \\[DP\\]"
			| get-property caption | equals "Beispiel: NoKyTo GmbH 2018 [DP]" | verify-true
		does-exist-in-other-perspecive "Modernisierter IT-Grundschutz" "Modernisierter IT-Grundschutz"
			"importierte Objekte/MODPLAST \\[SerNet-DM\\]" "MODPLAST [SerNet-DM]"
		does-exist-in-other-perspecive "GS-Modell" Grundschutzmodell "importierte Objekte/  RECPLAST \\[SerNet\\]"
			"RECPLAST [SerNet]"
	}
}
proc "close-and-reopen-query" [val filename] {
	get-editor $filename | close
	set-dialog-result File [concat $reportdir [get-java-property "file.separator"] $filename]
	get-menu -path "Datei/Report-Abfrage/Abfrage öffnen..." | click
}
proc "exec-and-export" [val reportname] [val scopelist] [val filename] {
	get-editor $reportname | get-button "Abfrage ausführen (CSV)..." | click
	$scopelist | each [val name] {
		get-window -class CsvExportDialog | get-group "" | get-button $name | check
	}
	set-dialog-result File [concat $reportdir [get-java-property "file.separator"] $filename]
	with [get-window -class CsvExportDialog] {
		get-button "Datei wählen" | click
		get-button OK | click
	}
}
proc "save-report" [val filename] {
	set-dialog-result File [concat $reportdir [get-java-property "file.separator"] $filename]
	get-eclipse-window | key-type "M1+s"
}
proc "new-query" [val selection1] [val selection2] {
	get-menu -path "Datei/Report-Abfrage/Neue Abfrage" | click
	with [get-editor "Neue Abfrage [UNGÜLTIG]"] {
		get-combo -after [get-label "Verknüpfungen"] | select $selection1
		get-combo -after [get-label "Verknüpfungen"] -index 2 | select $selection2
	}
}
proc "copy-edit-end" [val selection] {
	with [get-editor "Neue Abfrage"] {
		get-button "Letzte Spalte kopieren" | click
		get-combo -after [get-label "Verknüpfungen"] -index 45 | select $selection
	}
}
log "test_6.1-6.2"
new-query "Asset (ISM: asset)" "Art des Assets"
copy-edit-last 5 Beschreibung
edit-empty-row 7 "/" "Szenario (ISM: incident_scenario)" Titel
copy-edit-last 15 "Erklärung"
edit-empty-row 17 ":" "Person (ISM: person-iso)" Titel
copy-edit-last 25 Beschreibung
edit-empty-row 27 "<" "Assets (ISM: assetgroup)" Titel
copy-edit-last 35 Beschreibung
edit-empty-row 37 ">" "Control (ISM: control)" "Abkürzung"
copy-edit-end Beschreibung
save-report "TestReportISM.vlt"

log "6.1-6.2: building 2nd query"

new-query "Anwendung (GS neu: bp_application)" "Kürzel"
copy-edit-last 5 Beschreibung
edit-empty-row 7 "/" "Anwendung (GS neu: bp_application)" "Kürzel"
copy-edit-last 15 Anzahl
edit-empty-row 17 ":" "IT-System (GS neu: bp_itsystem)" Titel
copy-edit-last 25 Beschreibung
edit-empty-row 27 "<" "Anwendungen (GS neu: bp_application_group)" Titel
copy-edit-last 35 Beschreibung
edit-empty-row 37 ">" "Gefährdungen (GS neu: bp_threat_group)" Titel
copy-edit-end Beschreibung
save-report "TestReportMod.vlt"

new-query "Client (GS alt: client)" "Kürzel"
copy-edit-last 5 "Erläuterung"
edit-empty-row 7 "/" "Anwendung (GS alt: anwendung)" "Kürzel"
copy-edit-last 15 "Erläuterung"
edit-empty-row 17 ":" "IT-Verbund (GS alt: itverbund)" Titel
copy-edit-last 25 Beschreibung
edit-empty-row 27 "<" "IT-Systeme: Clients (GS alt: clientskategorie)" Name
copy-edit-last 35 "<UUID>"
edit-empty-row 37 ">" "Risikoanalyse (GS alt: riskanalysis)" Name
copy-edit-end "<UUID>"
save-report "TestReportAlt.vlt"

close-and-reopen-query "TestReportISM.vlt"
close-and-reopen-query "TestReportMod.vlt"
close-and-reopen-query "TestReportAlt.vlt"

log "6.1-6.2: 6.2 started"

ism-import-vna "RECPLAST.vna"
ism-import-vna "Beispiel_NoKyTo_GmbH_2018.vna"
ism-import-vna "modplast-1.1.vna"

wait-for-imports-or-fail

log "6.1-6.2: starting to execute queries"

exec-and-export "TestReportAlt.vlt" [list "Beispiel: NoKyTo GmbH 2018" MODPLAST RECPLAST] "TestReportAlt-All.csv"
exec-and-export "TestReportAlt.vlt" [list "Beispiel: NoKyTo GmbH 2018"] "TestReportAlt.csv"

exec-and-export "TestReportMod.vlt" [list "Beispiel: NoKyTo GmbH 2018" MODPLAST RECPLAST] "TestReportMod-All.csv"
exec-and-export "TestReportAlt.vlt" [list MODPLAST] "TestReportMod.csv"

exec-and-export"TestReportISM.vlt" [list "Beispiel: NoKyTo GmbH 2018" MODPLAST RECPLAST] "TestReportISM-All.csv"
exec-and-export "TestReportAlt.vlt" [list RECPLAST] "TestReportISM.csv"
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
